apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

ext.libArtifactId = 'base-library-ui'
ext.libPackageName = 'org.zuzuk.ui'
ext.libVersion = '1.0.11'

android {
    compileSdkVersion 21
    buildToolsVersion "21.0.2"

    defaultConfig {
        applicationId libPackageName
        minSdkVersion 7
        targetSdkVersion 21
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    compile project(':base-library')

    /* Common */
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:20.0.0'

    /* Image loading */
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                if (project.hasProperty("bintrayApiKey")) {
                    repository(url: "https://api.bintray.com/maven/zuzuk/maven/base-library-ui") {
                        authentication(
                                userName: bintrayUserName,
                                password: bintrayApiKey
                        )
                    }
                }

                pom.groupId = libPackageName
                pom.artifactId = libArtifactId
                pom.version = libVersion
                pom.name = libArtifactId
            }
        }
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java
    }

    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from 'src/main/java'
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}