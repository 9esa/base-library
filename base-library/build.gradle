buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'

repositories {
    jcenter()
}

ext.libArtifactId = 'base-library'
ext.libPackageName = 'org.zuzuk'
ext.libVersion = '1.0.12'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId libPackageName
        minSdkVersion 7
        targetSdkVersion 20
        versionCode 2
        versionName libVersion
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from 'src'
}

configurations{
    sourceArchives
}

artifacts{
    sourceArchives sourceJar
}

dependencies {
    /* Common */
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:20.0.0'

    /* Image loading */
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'

    /* Database working */
    compile 'com.j256.ormlite:ormlite-android:4.48'

    /* Network requests */
    compile 'com.octo.android.robospice:robospice:1.4.14'

    compile('com.google.http-client:google-http-client-android:1.17.0-rc') {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
    }
    compile('com.google.http-client:google-http-client-jackson2:1.17.0-rc') {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
    }
}

uploadArchives {
    repositories {
        mavenDeployer {

            configuration = configurations.sourceArchives

            repository(url: "https://api.bintray.com/maven/zuzuk/maven/base-library") {
                authentication(
                        userName: bintrayUserName,
                        password: bintrayApiKey
                )
            }
            pom.groupId = libPackageName
            pom.artifactId = libArtifactId
            pom.version = libVersion
        }
    }
}
