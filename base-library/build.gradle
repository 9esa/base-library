apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

ext.libArtifactId = 'base-library'
ext.libPackageName = 'org.zuzuk'
ext.libVersion = '1.1.7'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 20
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

dependencies {
    /* Common */
    provided 'com.android.support:appcompat-v7:20.0.0'

    /* Image loading */
    provided 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'

    /* Database working */
    provided 'com.j256.ormlite:ormlite-android:4.48'

    /* Serialization */
    provided 'de.ruedigermoeller:fst:2.22'

    /* Network requests */
    provided 'com.octo.android.robospice:robospice:1.4.14'
    provided('com.google.http-client:google-http-client-android:1.19.0') {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
    }
    provided('com.google.http-client:google-http-client-jackson2:1.19.0') {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
    }
}
afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://api.bintray.com/maven/zuzuk/maven/base-library") {
                    if (project.hasProperty("bintrayApiKey")) {
                        authentication(
                                userName: bintrayUserName,
                                password: bintrayApiKey
                        )
                    }
                }

                pom.groupId = libPackageName
                pom.artifactId = libArtifactId
                pom.version = libVersion
                pom.name = libArtifactId
            }
        }
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java
    }

    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from 'src/main/java'
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
